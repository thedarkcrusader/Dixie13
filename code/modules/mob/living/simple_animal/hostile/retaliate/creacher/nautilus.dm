/mob/living/simple_animal/hostile/retaliate/nautilus
	icon = 'icons/roguetown/mob/monster/nautilus.dmi'
	name = "nautilus"
	desc = ""
	icon_state = "nautilus"
	icon_living = "nautilus"
	icon_dead = "nautilus_dead"
	SET_BASE_PIXEL(-16, 0)

	faction = list(FACTION_SEA)
	speak_emote = list("gurgles", "bubbles")
	emote_see = list("writhes its tentacles")
	emote_hear = list("gurgles wetly", "makes bubbling sounds")
	move_to_delay = 3
	vision_range = 5
	see_in_dark = 5
	aggro_vision_range = 5


	// a pretty hefty haul, well worth the hunt if you can actually butcher it properly
	botched_butcher_results = list(/obj/item/reagent_containers/food/snacks/meat/mince/fish = 2,
						/obj/item/carvedgem/shell/rawshell = 2,
						/obj/item/gem/pearl = 1)
	butcher_results = list(/obj/item/reagent_containers/food/snacks/meat/mince/fish = 3,
						/obj/item/carvedgem/shell/rawshell = 2,
						/obj/item/gem/pearl = 1,
						/obj/item/gem/pearl_blue = 1)
	perfect_butcher_results = list(/obj/item/reagent_containers/food/snacks/meat/mince/fish = 4,
						/obj/item/carvedgem/shell/rawshell = 3,
						/obj/item/gem/pearl = 2,
						/obj/item/gem/pearl_blue = 1)


	gender = PLURAL
	health = 600
	maxHealth = 600
	food_type = list(/obj/item/reagent_containers/food/snacks/meat,
					/obj/item/reagent_containers/food/snacks/fish,
					/obj/item/organ,
					/obj/item/bodypart)

	base_intents = list(/datum/intent/simple/nautilus_lash)
	attack_sound = list('sound/combat/wooshes/whip_crack1.ogg','sound/combat/wooshes/whip_crack2.ogg','sound/combat/wooshes/whip_crack3.ogg')
	melee_damage_lower = 20
	melee_damage_upper = 30

	base_constitution = 14
	base_strength = 12
	base_speed = 6
	force_threshold = 15

	environment_smash = ENVIRONMENT_SMASH_STRUCTURES
	simple_detect_bonus = 20
	retreat_distance = 0
	minimum_distance = 0
	deaggroprob = 0
	defprob = 15
	defdrain = 10
	del_on_deaggro = 999 SECONDS
	retreat_health = 0

	dodgetime = 50
	aggressive = 1
	stat_attack = UNCONSCIOUS
	remains_type = /obj/effect/decal/remains/nautilus
	body_eater = TRUE

	ai_controller = /datum/ai_controller/nautilus
	dendor_taming_chance = DENDOR_TAME_PROB_NONE

	var/wrestling_bonus = SKILL_LEVEL_EXPERT

	var/hiding = FALSE

/mob/living/simple_animal/hostile/retaliate/nautilus/Initialize()
	. = ..()
	//AddComponent(/datum/component/ai_aggro_system)

	update_appearance(UPDATE_OVERLAYS)
	ADD_TRAIT(src, TRAIT_NOHANDGRABS, TRAIT_GENERIC)
	ADD_TRAIT(src, TRAIT_STRONG_GRABBER, TRAIT_GENERIC)
	ADD_TRAIT(src, TRAIT_GOOD_SWIM, TRAIT_GENERIC)

/mob/living/simple_animal/hostile/retaliate/nautilus/taunted(mob/user)
	emote("aggro")
	return

/mob/living/simple_animal/hostile/retaliate/nautilus/hide()
	if(!hiding)
		sleep(1 SECONDS)
		icon_state = "nautilus_hide"
		force_threshold = 80
		hiding = TRUE

/mob/living/simple_animal/hostile/retaliate/nautilus/ambush()
	if(hiding)
		icon_state = "nautilus"
		sleep(1 SECONDS)
		force_threshold = 15
		hiding = FALSE

//this is an impermanent solution with an indefinite fix date
/mob/living/simple_animal/hostile/retaliate/nautilus/apply_damage(damage, damagetype, def_zone, blocked, forced)
	if(damage > force_threshold)
		return ..()

/mob/living/simple_animal/hostile/retaliate/nautilus/AttackingTarget(mob/living/passed_target)
	. = ..()
	if(!(. && isliving(target)))
		return
	var/mob/living/L = target

	var/grappledAlready = FALSE
	for(var/obj/item/grabbing/G in get_contents())
		if(!iscarbon(G.grabbed))
			continue
		if(target == G.grabbed)
			grappledAlready = TRUE

		if(prob(70))
			continue
		var/mob/living/carbon/C = G.grabbed
		if(C.body_position == STANDING_UP)
			C.Knockdown(20)
			visible_message(span_userdanger("[src] tackles [C] to the ground!"))
		else
			var/choke = /datum/intent/grab/choke
			var/twist = /datum/intent/grab/twist
			var/smash = /datum/intent/grab/smash

			G.update_grab_intents()
			var/list/grabIntents = G.possible_item_intents
			grabIntents &= list(choke, twist, smash)
			if(!length(grabIntents))
				continue
			if(grabIntents.Find(choke) && get_location_accessible(C, BODY_ZONE_PRECISE_NECK))
				var/choke_damage = STASTR * 0.5 // less than the average choke
				choke_damage *= 1.2
				if(C.pulling == src && C.grab_state >= GRAB_AGGRESSIVE)
					choke_damage *= 0.95
				C.adjustOxyLoss(choke_damage)
				C.visible_message(span_danger("[src] [pick("chokes", "strangles")] [C][G.chokehold ? " with a chokehold" : ""]!"), \
						span_userdanger("[src] [pick("chokes", "strangles")] me[G.chokehold ? " with a chokehold" : ""]!"), span_hear("I hear a sickening sound of pugilism!"), COMBAT_MESSAGE_RANGE, src)
			else if (pick(grabIntents) == twist)
				G.twistlimb(src)
			else
				G.smashlimb(C, src, C.loc)

	if(!grappledAlready && prob(30) && start_pulling(L, suppress_message = TRUE, accurate = TRUE))
		visible_message(span_boldwarning("[src] wraps their tentacles around [L]!"))
		L.Immobilize(10)

/mob/living/simple_animal/hostile/retaliate/nautilus/simple_limb_hit(zone)
	if(!zone)
		return ""
	switch(zone)
		if(BODY_ZONE_PRECISE_R_EYE)
			return "eyestalk"
		if(BODY_ZONE_PRECISE_L_EYE)
			return "eyestalk"
		if(BODY_ZONE_PRECISE_NOSE)
			return "beak"
		if(BODY_ZONE_PRECISE_MOUTH)
			return "beak"
		if(BODY_ZONE_HEAD)
			return "beak"
		if(BODY_ZONE_PRECISE_SKULL)
			return "shell"
		if(BODY_ZONE_PRECISE_EARS)
			return "head"
		if(BODY_ZONE_PRECISE_L_HAND)
			return "tentacles"
		if(BODY_ZONE_PRECISE_R_HAND)
			return "tentacles"
		if(BODY_ZONE_PRECISE_L_FOOT)
			return "tentacles"
		if(BODY_ZONE_PRECISE_R_FOOT)
			return "tentacles"
		if(BODY_ZONE_R_LEG)
			return "tentacles"
		if(BODY_ZONE_L_LEG)
			return "tentacles"
		if(BODY_ZONE_R_ARM)
			return "tentacles"
		if(BODY_ZONE_L_ARM)
			return "tentacles"
		else
			return "shell"

/mob/living/simple_animal/hostile/retaliate/nautilus/get_wrestling_bonuses()
	return wrestling_bonus

/datum/intent/simple/nautilus_lash
	name = "tendril lash"
	icon_state = "inlash"
	attack_verb = list("lashes", "whips")
	animname = "slash"
	blade_class = BCLASS_LASHING
	hitsound = "smallslash"
	chargetime = 0
	penfactor = 8
	canparry = FALSE
	item_damage_type = "slash"


/obj/effect/decal/remains/nautilus
	icon_state = "nautilus_dead"
	icon = 'icons/roguetown/mob/monster/nautilus.dmi'
